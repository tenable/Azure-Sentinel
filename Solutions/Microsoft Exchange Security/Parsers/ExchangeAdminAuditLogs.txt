// Title:           ESI - Exchange Admin Audit Logs Parser
// Author:          Microsoft
// Version:         1.0
// Last Updated:    15/11/2022
// Comment:  
//      v1.0 : 
//          - Function initilisation for Sentinel Solution
//  
// DESCRIPTION:
// This parser takes raw Exchange Admin Audit Logs and add elements like ESI Environment, VIP information, sensitive information, etc...
//
// USAGE:
// 1. Open Log Analytics/Microsoft Sentinel Logs blade. Copy the query below and paste into the Logs query window. 
// 2. Click the Save button above the query. A pane will appear on the right, select "as Function" from the drop down. Enter the Function Name "ExchangeAdminAuditLogs".
// 3. Function App usually take 10-15 minutes to activate. You can then use Function Alias for other queries
//
// DEPENDENCY:
// This parser is linked to "ExchangeVIP" whatchlist
//
// REFERENCE: 
// Using functions in Azure monitor log queries: https://docs.microsoft.com/azure/azure-monitor/log-query/functions
//
// LOG SAMPLES:
// This parser assumes that MS Exchange Management Logs from Exchange Servers Event Logs are collected in Log Analytics.
//
//
let cVIPs = _GetWatchlist('ExchangeVIP') | project tostring(canonicalName) ;
let sVIPs = _GetWatchlist('ExchangeVIP') | project tostring(sAMAccountName) ;
let CmdletCheck = externaldata (Cmdlet:string, UserOriented:string, RestrictToParameter:string, Parameters:string)[h"https://raw.githubusercontent.com/nlepagnez/ESI-PublicContent/main/Operations/Watchlists/CmdletWatchlist.csv"]with(format="csv",ignoreFirstRecord=true);
let SensitiveCmdlets = externaldata (Cmdlet:string, UserOriented:string, RestrictToParameter:string, Parameters:string)[h"https://raw.githubusercontent.com/nlepagnez/ESI-PublicContent/main/Operations/Watchlists/CmdletWatchlist.csv"]with(format="csv",ignoreFirstRecord=true) | project tostring(Cmdlet) ;
let Env = ExchangeConfiguration(SpecificSectionList="ESIEnvironment")
| extend DomainFQDN_ = tostring(CmdletResultValue.DomainFQDN)
| project DomainFQDN_, ESIEnvironment;
let MSExchange_Management = (){
    Event
    | where EventLog == 'MSExchange Management'
    | where EventID in (1,6) // 1 = Success, 6 = Failure
    | parse ParameterXml with '<Param>' CmdletName '</Param><Param>' CmdletParameters '</Param><Param>' Caller '</Param><Param>' *
    | extend TargetObject = iif( CmdletParameters has "-Identity ", split(split(CmdletParameters,'-Identity ')[1],'"')[1], iif( CmdletParameters has "-Name ", split(split(CmdletParameters,'-Name ')[1],'"')[1], ""))
    | extend Status = case( EventID == 1, 'Success', 'Failure')
    | extend IsVIP = iif(TargetObject in (cVIPs) or TargetObject in (sVIPs), true, false)
    | extend CmdletNameJoin = tolower(CmdletName)
    | join kind=leftouter  ( 
        CmdletCheck
    | extend CmdletNameJoin = tolower(Cmdlet)
    ) on CmdletNameJoin
    | extend DomainEnv = replace_string(Computer,strcat(tostring(split(Computer,'.',0)[0]),'.'),'')
    | join kind=leftouter  ( 
        Env
    ) on $left.DomainEnv == $right.DomainFQDN_
    | extend ESIEnvironment = iif (isnotempty(ESIEnvironment), ESIEnvironment, strcat("Unknown-",DomainEnv))
    | extend IsSenstiveCmdlet = iif( isnotempty(CmdletNameJoin1) , true, false) 
    | extend IsRestrictedCmdLet = iif(IsSenstiveCmdlet == true, iif( RestrictToParameter == "Yes", true, false), dynamic(null))
    | extend RestrictedParameters = iif(IsSenstiveCmdlet == true, split(tolower(Parameters),';'), dynamic(null))
    | extend ExtractedParameters = iif(IsSenstiveCmdlet == true,extract_all(@"\B(-\w+)", tolower(CmdletParameters)), dynamic(null))
    | extend IsSenstiveCmdletParameters = iif(IsSenstiveCmdlet == true,iif( array_length(set_difference(ExtractedParameters,RestrictedParameters)) == array_length(ExtractedParameters), false, true ) , false)
    | extend IsSensitive = iif( ( IsSenstiveCmdlet == true and IsRestrictedCmdLet == false ) or (IsSenstiveCmdlet == true and IsRestrictedCmdLet == true and IsSenstiveCmdletParameters == true ), true, false )
    //| project TimeGenerated,Computer,Status,Caller,TargetObject,IsVIP,CmdletName,CmdletParameters,IsSenstiveCmdlet,IsRestrictedCmdLet,ExtractedParameters,RestrictedParameters,IsSenstiveCmdletParameters
    | project TimeGenerated,Computer,Status,Caller,TargetObject,IsVIP,CmdletName,CmdletParameters,IsSenstiveCmdlet,IsRestrictedCmdLet,ExtractedParameters,RestrictedParameters,IsSenstiveCmdletParameters,IsSensitive,UserOriented, ESIEnvironment
};
MSExchange_Management
