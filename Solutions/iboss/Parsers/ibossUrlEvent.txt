// Usage Instructions:
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as ibossUrlEvent and specifying Legacy Category.
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. ibossUrlEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
CommonSecurityLog
| where DeviceVendor == "iboss" and FlexString2 == "URL"
| extend DeviceCustomNumber1 = coalesce(column_ifexists("FieldDeviceCustomNumber1", long(null)),DeviceCustomNumber1),
         DeviceCustomNumber2 = coalesce(column_ifexists("FieldDeviceCustomNumber2", long(null)),DeviceCustomNumber2)
| project-rename EventVendor=DeviceVendor
                , EventProduct=DeviceProduct
                , EventProductVersion=DeviceVersion
                , EventResult=DeviceEventClassID
                , EventResultDetails=FlexNumber1
                , DvcAction=DeviceAction
                , RuleName=FlexString1
                , SrcPortNumber=SourcePort
                , SrcIpAddr=SourceIP
                , SrcMacAddr=SourceMACAddress
                , SrcUsername=SourceUserName
                , SrcBytes=SentBytes
                , DstPortNumber=DestinationPort
                , DstIpAddr=DestinationIP
                , DstBytes=ReceivedBytes
                , Domain=DestinationHostName
                , Url=RequestURL
                , UrlCategory=DeviceCustomString2
                , HttpRequestMethod=RequestMethod
                , HttpUserAgent=RequestClientApplication
                , FileSHA256=DeviceCustomString3
                , ThreatName=DeviceCustomString1
                , MalwareDetected=DeviceCustomNumber1
                , CNCDetected=DeviceCustomNumber2
| extend NetworkBytes=SrcBytes+DstBytes
        , EventTime=todatetime(DeviceCustomDate1)
| project-away DeviceCustom*
            , FlexNumber*
            , FlexString*
