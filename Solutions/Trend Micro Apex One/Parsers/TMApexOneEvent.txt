// Usage Instruction : 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias as TMApexOneEvent.
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. TMApexOneEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
CommonSecurityLog
| where DeviceVendor == "Trend Micro"
| where DeviceProduct == "Apex Central"
| extend DeviceCustomNumber1 = coalesce(column_ifexists("FieldDeviceCustomNumber1", long(null)),DeviceCustomNumber1),
         DeviceCustomNumber2 = coalesce(column_ifexists("FieldDeviceCustomNumber2", long(null)),DeviceCustomNumber2),
         DeviceCustomNumber3 = coalesce(column_ifexists("FieldDeviceCustomNumber3", long(null)),DeviceCustomNumber3),
         ExternalID = coalesce(column_ifexists("ExtID", ""),tostring(ExternalID))
| extend packed = pack (DeviceCustomNumber1Label, DeviceCustomNumber1,
    DeviceCustomNumber2Label, DeviceCustomNumber2,
    DeviceCustomString1Label, DeviceCustomString1,
    DeviceCustomString2Label, DeviceCustomString2,
    DeviceCustomString3Label, DeviceCustomString3,
    DeviceCustomString4Label, DeviceCustomString4,
    DeviceCustomString5Label, DeviceCustomString5,
    DeviceCustomString6Label, DeviceCustomString6,
    DeviceCustomDate1Label, DeviceCustomDate1,
    DeviceCustomDate2Label, DeviceCustomDate2)
| evaluate bag_unpack(packed)
| project-rename EventVendor=DeviceVendor,
    EventProduct=DeviceProduct,
    EventProductVersion=DeviceVersion,
    EventSubType=DeviceEventClassID,
    EventMessage=Activity,
    EventSeverity=LogSeverity,
    EventOriginalUid=DeviceExternalID,
    EventEndTime=ReceiptTime,
    DstDvcHostname=DestinationHostName,
    DstIpAddr=DestinationIP,
    DstUserName=DestinationUserName,
    DstPortNumber=DestinationPort,
    DstServiceName=DestinationServiceName,
    SrcPortNumber=SourcePort,
    SrcIpAddr=SourceIP,
    SrcDvcHostname=SourceHostName,
    SrcServiceName=SourceServiceName,
    SrcUserName=SourceUserName,
    SrcProcessName=SourceProcessName,
    SrcMacAddr=SourceMACAddress,
    DvcAction=DeviceAction,
    DvcHostname=DeviceName,
    DvcProcessName=ProcessName,
    FileHashSha1=FileHash,
    UrlOriginal=RequestURL,
    NetworkDirection=CommunicationDirection
| extend Command = iif(DeviceCustomString3Label == "Command", DeviceCustomString3, "")
| extend ActionResult = iif(DeviceCustomString5Label == "ActionResult", DeviceCustomString5, "")
| extend Event_Type = iif(DeviceCustomNumber2Label == "Event_Type", DeviceCustomNumber2, long(null))
| extend VirusName = iif(DeviceCustomString1Label == "VirusName", DeviceCustomString1, "")
| extend Policy = iif(DeviceCustomString2Label == "Policy", DeviceCustomString2, "")
| extend ProcessCommandLine = iif(DeviceCustomString4Label == "ProcessCommandLine", DeviceCustomString4, "")
| project-away DeviceCustomNumber1Label,
    DeviceCustomNumber1,
    DeviceCustomNumber2Label,
    DeviceCustomNumber2,
    DeviceCustomString1Label,
    DeviceCustomString1,
    DeviceCustomString2Label,
    DeviceCustomString2,
    DeviceCustomString3Label,
    DeviceCustomString3,
    DeviceCustomString4Label,
    DeviceCustomString4,
    DeviceCustomString5Label,
    DeviceCustomString5,
    DeviceCustomString6Label,
    DeviceCustomString6,
    DeviceCustomDate1Label,
    DeviceCustomDate1,
    DeviceCustomDate2Label,
    DeviceCustomDate2
