// Usage Instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as ClarotyEvent and specifying Legacy Category.
// This function maps Claroty events to Microsoft Sentinel Information Model (ASIM) (https://docs.microsoft.com/azure/sentinel/normalization).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. ClarotyEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
CommonSecurityLog
| where DeviceVendor =~ 'Claroty'
| extend EventVendor = 'Claroty'
| extend EventProduct = 'Claroty'
| extend EventSchemaVersion = 0.2
| extend EventCount = 1
| extend DeviceCustomNumber1 = coalesce(column_ifexists("FieldDeviceCustomNumber1", long(null)),DeviceCustomNumber1),
         DeviceCustomNumber2 = coalesce(column_ifexists("FieldDeviceCustomNumber2", long(null)),DeviceCustomNumber2),
         DeviceCustomNumber3 = coalesce(column_ifexists("FieldDeviceCustomNumber3", long(null)),DeviceCustomNumber3),
         ExternalID = coalesce(column_ifexists("ExtID", ""),tostring(ExternalID))
| extend packed = pack(DeviceCustomNumber1Label, DeviceCustomNumber1
                     , DeviceCustomNumber2Label, DeviceCustomNumber2
                     , DeviceCustomNumber3Label, DeviceCustomNumber3
                     , DeviceCustomString1Label, DeviceCustomString1
                     , DeviceCustomString2Label, DeviceCustomString2
                     , DeviceCustomString3Label, DeviceCustomString3
                     , DeviceCustomString4Label, DeviceCustomString4
                     , DeviceCustomString5Label, DeviceCustomString5
                     , DeviceCustomString6Label, DeviceCustomString6
                     , DeviceCustomDate1Label, DeviceCustomDate1
                     , DeviceCustomDate2Label, DeviceCustomDate2)
| evaluate bag_unpack(packed)
| parse-kv AdditionalExtensions as (cs7Label:string, cs7:string, cs8Label:string, cs8:string, cs9Label:string, cs9:string, cs10Label:string, cs10:string, cat:string) with (pair_delimiter=';', kv_delimiter='=')
| extend packed2 = pack(cs7Label, cs7
                     , cs8Label, cs8
                     , cs9Label, cs9
                     , cs10Label, cs10)
| evaluate bag_unpack(packed2)
| extend EventEndTime = todatetime(ReceiptTime),
         cat = coalesce(column_ifexists("DeviceEventCategory", ""),cat)
| project-rename EventProductVersion=DeviceVersion
                , EventSubType=cat
                , EventOriginalType=DeviceEventClassID
                , EventSeverity=LogSeverity
                , EventMessage=Message
                , DstPortNumber=DestinationPort
                , DstIpAddr=DestinationIP
                , DstDvcHostname=DestinationHostName
                , DstUserName=DestinationUserName
                , DvcIpAddr=DeviceAddress
                , DvcHostname=DeviceName
                , DstMacAddr=DestinationMACAddress
                , NetworkApplicationProtocol=Protocol
                , SrcPortNumber=SourcePort
                , SrcIpAddr=SourceIP
                , SrcMacAddr=SourceMACAddress
                , EventId=ExternalID
                , SrcDvcHostname=SourceHostName
| extend EventType=Activity
| project-away AdditionalExtensions
            , Activity
            , ReceiptTime
            , DeviceVendor
            , DeviceProduct
            , DeviceCustomNumber1
            , DeviceCustomNumber1Label
            , DeviceCustomNumber2
            , DeviceCustomNumber2Label
            , DeviceCustomNumber3
            , DeviceCustomNumber3Label
            , DeviceCustomString1
            , DeviceCustomString1Label
            , DeviceCustomString2
            , DeviceCustomString2Label
            , DeviceCustomString3
            , DeviceCustomString3Label
            , DeviceCustomString4
            , DeviceCustomString4Label
            , DeviceCustomString5
            , DeviceCustomString5Label
            , DeviceCustomString6
            , DeviceCustomString6Label
            , cs7Label
            , cs7
            , cs8Label
            , cs8
            , cs9Label
            , cs9
            , cs10Label
            , cs10