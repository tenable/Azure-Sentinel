Parser:
  Title: Authentication ASIM filtering parser for AWS sign-in logs
  Version: '0.1.1'
  LastUpdated: July 28, 2022
Product:
  Name: AWS
Normalization:
  Schema: Authentication
  Version: '0.1.1'
References:
- Title: ASIM Authentication Schema
  Link: https://aka.ms/ASimAuthenticationDoc
- Title: ASIM
  Link: https:/aka.ms/AboutASIM
Description: |
  This ASIM parser supports filtering and normalizing Amazon Web Service sign in logs, stored in the  AWSCloudTrail table, to the ASIM Authentication schema.
ParserName: vimAuthenticationAWSCloudTrail
ParserParams:
  - Name: starttime
    Type: datetime
    Default: datetime(null)
  - Name: endtime
    Type: datetime
    Default: datetime(null)
  - Name: targetusername_has
    Type: string
    Default: '*'
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let usertype_lookup = datatable (UserIdentityType:string, TargetUserType:string, TargetUsername:string) [
    'Root', 'Admin', 'Root',
    'IAMUser', 'Regular', '',
    'AWSService',  'Service Principal', ''
  ];
  let eventresultdetails_lookup = datatable (ErrorMessage:string, EventResultDetails:string) [
    'No username found in supplied account', 'No such user or password',
    'Failed authentication', 'Other'
  ];
  let parser= (
    starttime:datetime=datetime(null), 
    endtime:datetime=datetime(null),
    targetusername_has:string="*", 
    disabled:bool=false
  )
  {
    AWSCloudTrail | where not(disabled)
    //  -- Pre filtering
    | where 
      (isnull(starttime) or TimeGenerated >= starttime) 
      and (isnull(endtime) or TimeGenerated <= endtime) 
      and EventName == 'ConsoleLogin'
      and (targetusername_has=='*' or SessionIssuerUserName has targetusername_has or UserIdentityUserName has targetusername_has)
    //  -- end pre-filtering
    | extend
        EventVendor = 'AWS',
        EventProduct='CloudTrail',
        EventCount=int(1),
        EventSchemaVersion='0.1.1',
        EventSchema = 'Authentication',
        EventResult= iff (ResponseElements has_cs 'Success', 'Success', 'Failure'),
        EventStartTime=TimeGenerated,
        EventEndTime=TimeGenerated,
        EventType='Logon',
        TargetUserIdType = 'AWSId',
        LogonMethod=iff(AdditionalEventData has '"MFAUsed": "Yes"', 'MFA',''),
        SrcDeviceType = iff(AdditionalEventData has '"MobileVersion":"Yes"', 'Mobile Device', 'Computer'),
        TargetUsernameType = iff (UserIdentityUserName contains '@', 'UPN', 'Simple'),
        ArnUsername = split(UserIdentityArn, '/')[-1]
    | parse AdditionalEventData with * '"LoginTo":"' TargetUrl:string '"' *
    | lookup eventresultdetails_lookup on ErrorMessage
    | project-rename
        EventOriginalUid= AwsEventId,
        EventOriginalResultDetails= ErrorMessage,
        EventProductVersion = EventVersion,
        TargetSubscription = UserIdentityAccountId,
        TargetUserId = UserIdentityPrincipalid, // or should I use UserIdentityArn?
        SrcIpAddr = SourceIpAddress,
        TargeCloudRegion = AWSRegion,
        HttpUserAgent = UserAgent
      | lookup usertype_lookup on UserIdentityType // extends TargetUserType
      | extend 
        TargetUsername = coalesce(SessionIssuerUserName, UserIdentityUserName, ArnUsername, TargetUsername),
        LogonTarget=tostring(split(TargetUrl,'?')[0]),
        EventSeverity = iff(EventResult == 'Failure', 'Low','Informational')
      // -- Aliases
      | extend
          User=TargetUsername,
          Dvc=EventVendor,
          Dst=LogonTarget
      // -- Backward compatibility 
      | extend
          SrcDvcIpAddr = SrcIpAddr
      | project-away EventSource, EventTypeName, EventName, ResponseElements, AdditionalEventData, Session*, Category, ErrorCode, Aws*, ManagementEvent, OperationName, ReadOnly, RequestParameters, Resources, ServiceEventDetails, SharedEventId, SourceSystem, UserIdentity*, VpcEndpointId, APIVersion, RecipientAccountId, ArnUsername
      };
    parser (starttime=starttime, endtime=endtime, targetusername_has=targetusername_has, disabled=disabled) 
